#!/bin/python 

import os
from pathlib import Path
from scripts.samples import Samples

samples = Samples(config)

REFERENCES = config["references"]
OUTPUT_BASE_PATH = config["paths"]["output_dir"]

def get_sample_string(identifiers):
    return "_".join(["{" + label + "}" for label in identifiers])

SAMPLE_WILDCARDS = get_sample_string(config['metadata']['identifiers'])

print(samples.metadata)

localrules: link_files, write_samples_spreadsheet, all  # local execution of non-demanding rules

wildcard_constraints:
    sample_type="[A-z0-9_-]+",
    individual="[A-z0-9_-]+"

rule all:
    input:
        samples.targets


rule link_files:
    """Link files with filename pattern required for Cellranger
    
    Note: output must match target_templates in configuration YAML.
    
    """
    input:
        lambda wildcards: samples.filter_by_wildcards(
            wildcards=wildcards,
            data=samples.metadata,
            column="FastQ Path",

        )
    output:
        directory(f"{OUTPUT_BASE_PATH}/linked_files/{{individual}}/{{sample_type}}/")
    log:
        "logs/link_files_{individual}_{sample_type}.log"
    run:
        log_fh  = open(log[0], "w")
        os.makedirs(output[0], exist_ok=True)
        for in_fastq_filepath in input:
            print(f"linking {in_fastq_filepath} to {output[0]}", file=log_fh)
            out_fastq_filepath = samples.metadata.loc[samples.metadata["FastQ Path"] == in_fastq_filepath]["CELLRANGER_FASTQ_PATH"].to_list()
            if len(out_fastq_filepath) == 1:
                out_fastq_filepath = out_fastq_filepath[0]
            os.symlink(in_fastq_filepath, Path(output[0], out_fastq_filepath))
        log_fh.close()

rule write_samples_spreadsheet:
    output:
        f"{OUTPUT_BASE_PATH}/{config['paths']['target_templates']['samples_sheet']}"
    log:
        "logs/write_samples_spreadsheet.log"
    run:
        with open(log[0], "w") as log_fh:
            print("storing samples metadata to {output[0]}", file=log_fh)
            samples.metadata.to_csv(output[0], index=False)
            print("done.", file=log_fh)

rule cellranger_count:
    """Run Cellranger count
    
    Note: output must match target_templates in configuration YAML. 
    """

    input:
        rules.link_files.outputgit
    output:
        outs_dir=f"{OUTPUT_BASE_PATH}/cellranger_count/{{individual}}_{{sample_type}}/outs"
    params:
        genome=REFERENCES["all_masked"],
        output_root_dir=lambda wildcards, output: Path(output[0]).parents[1],  # remove last 2 levels from output path
        extra=config["rules"]["cellranger_count"]["extra"]
    log:
        "logs/cellranger_count_{individual}_{sample_type}.log"
    threads: 16
    envmodules:
        "cellranger/6.1.1",
    shell:
        "cd {params.output_root_dir}; "
        "cellranger count "
        "--id {wildcards.individual}_{wildcards.sample_type} "  # letters, numbers, hyphens, and underscores
        "--transcriptome {params.genome} "
        "--fastqs {input} "
        "--localcores={threads} "
        "{params.extra} "
        "--sample {wildcards.individual}_{wildcards.sample_type} "
        "> {log}"
